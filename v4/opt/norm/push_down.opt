[PushDownSelectJoinLeft, Normalize]
(Select
    $input:(Join $left:^(Select) $right:* $on:*)
    (Filters $list:[ ... $condition:* & ^(IsCorrelated $condition $right) ... ])
)
=>
(Select
    ((OpName $input)
        (Select $left $condition)
        $right
        $on
    )
    (Filters (RemoveListItem $list $condition))
)

[PushDownSelectJoinSelectLeft, Normalize]
(Select
    $input:(Join (Select $inputInner:* (Filters [ ... $filterInner:* ... ])) $right:* $on:*)
    (Filters $list:[ ... $condition:* & ^(IsCorrelated $condition $right) ... ])
)
=>
(Select
    ((OpName $input)
        (Select $inputInner (Filters [$filterInner $condition]))
        $right
        $on
    )
    (Filters (RemoveListItem $list $condition))
)

[PushDownSelectJoinRight, Normalize]
(Select
    $input:(InnerJoin|InnerJoinApply $left:* $right:^(Select) $on:*)
    (Filters $list:[ ... $condition:* & ^(IsCorrelated $condition $left) ... ])
)
=>
(Select
    ((OpName $input)
        $left
        (Select $right $condition)
        $on
    )
    (Filters (RemoveListItem $list $condition))
)

[PushDownSelectJoinSelectRight, Normalize]
(Select
    $input:(InnerJoin|InnerJoinApply $left:* (Select $inputInner:* (Filters [ ... $filterInner:* ... ])) $on:*)
    (Filters $list:[ ... $condition:* & ^(IsCorrelated $condition $left) ... ])
)
=>
(Select
    ((OpName $input)
        $left
        (Select $inputInner (Filters [$filterInner $condition]))
        $on
    )
    (Filters (RemoveListItem $list $condition))
)

[PushDownJoinFilter, Normalize]
(Join
    $left:*
    $right:*
    (Filters $list:[ ... $condition:* ... ] & ^(IsCorrelated $condition $right))
)
=>
((OpName)
    (Select $left $condition)
    $right
    (Filters (RemoveListItem $list $condition))
)
